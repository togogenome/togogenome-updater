PREFIX insdc:   <http://ddbj.nig.ac.jp/ontologies/nucleotide/>
PREFIX obo:     <http://purl.obolibrary.org/obo/>

SELECT (COUNT(?feature) AS ?count)
FROM <http://togogenome.org/graph/refseq>
WHERE
{
 VALUES ?feature_types { insdc:Coding_Sequence insdc:Non_Coding_RNA insdc:Transfer_RNA insdc:Messenger_RNA }

 ?refseq rdf:type insdc:Entry ;
   insdc:sequence ?seq ;
   insdc:dblink ?bioproject .
 ?bioproject a insdc:BioProject ;
   rdfs:label ?bioproject_id .
 ?seq obo:RO_0002162/rdfs:label ?taxonomy_id .
 ?gene obo:so_part_of ?seq ;
   rdfs:label ?gene_label ;
   rdf:type insdc:Gene .
 ?feature obo:so_part_of ?gene ;
   rdfs:label ?feature_label ;
   rdf:type ?feature_types .
 OPTIONAL
 {
   ?feature rdfs:seeAlso ?protein .
   ?protein a insdc:Protein ;
     rdfs:label ?protein_id .
 }
}
