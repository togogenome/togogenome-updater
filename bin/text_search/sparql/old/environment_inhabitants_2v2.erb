SPARQL
PREFIX mccv: <http://purl.jp/bio/01/mccv#>
PREFIX meo: <http://purl.jp/bio/11/meo/>

SELECT
  ?meo_root
  (GROUP_CONCAT(DISTINCT ?strain_number; separator = " ") AS ?source_ids)
  (GROUP_CONCAT(DISTINCT ?strain_name; separator = " ") AS ?organism_names)
  (GROUP_CONCAT(DISTINCT ?isolation; separator = " ") AS ?isolations)
  (GROUP_CONCAT(DISTINCT ?meo_label; separator = " ") AS ?meo_labels)
FROM <http://togogenome.org/graph/taxonomy>
FROM <http://togogenome.org/graph/brc>
FROM <http://togogenome.org/graph/meo>
WHERE
{
  VALUES ?related_type { mccv:MCCV_000056 mccv:MCCV_000022 mccv:MCCV_000057 }

  { SELECT DISTINCT ?meo_root ?strain_id
    {
      VALUES ?meo_mapping { mccv:MCCV_000059 mccv:MCCV_000060 }
       GRAPH <http://togogenome.org/graph/meo>
      {
        ?meo_root a owl:Class .
      }
      ?meo_id rdfs:subClassOf* ?meo_root .
      ?strain_id ?meo_mapping ?meo_id .
      ?strain_id rdf:type mccv:MCCV_000001 .
    }
  }
  OPTIONAL { ?strain_id mccv:MCCV_000010 ?strain_number . }
  OPTIONAL { ?strain_id mccv:MCCV_000012 ?strain_name . }
  OPTIONAL { ?strain_id mccv:MCCV_000030 ?isolation . }
  OPTIONAL
  {
    ?strain_id mccv:MCCV_000059|mccv:MCCV_000060 ?meo_id .
    ?meo_id rdfs:label ?meo_label .
  }
} GROUP BY ?meo_root
;
