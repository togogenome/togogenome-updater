DEFINE sql:select-option "order"
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX insdc: <http://ddbj.nig.ac.jp/ontologies/nucleotide/>

SELECT DISTINCT ?name ?message
WHERE {
  GRAPH <http://togogenome.org/graph/tgup>
  {
    ?togo_gene skos:exactMatch <%= feature_uri %> .
    ?togo_gene rdfs:seeAlso ?id_upid .
    ?id_upid rdfs:seeAlso ?protein .
  }
  GRAPH <http://togogenome.org/graph/uniprot>
  {
    ?protein a up:Protein ;
        up:annotation ?annotation .
    {
      ?annotation a up:Annotation .
      BIND(STR('Miscellaneous') AS ?name) .
      ?annotation rdfs:comment ?message .
    }
    UNION
    {
      ?annotation a up:Subcellular_Location_Annotation .
      up:Subcellular_Location_Annotation rdfs:label ?name .
      ?annotation up:locatedIn ?located_in .
      ?located_in up:cellularComponent ?location .
      ?location up:alias ?message .
    }
    UNION
    {
      ?annotation a ?type .
      ?type rdfs:subClassOf up:Annotation .
      FILTER (?type != up:Subcellular_Location_Annotation)
      ?type rdfs:label ?name .
      ?annotation rdfs:comment ?message .
    }
  }
}
