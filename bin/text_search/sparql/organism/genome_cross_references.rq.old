#This SPARQL too slow
SPARQL
DEFINE sql:select-option "order"
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX insdc: <http://ddbj.nig.ac.jp/ontologies/nucleotide/>

SELECT ?tax_id ("^@" AS ?delimit1)
  GROUP_CONCAT(DISTINCT ?desc; separator = "|||") AS ?refseq_definitions ("^@" AS ?delimit2)
  GROUP_CONCAT(DISTINCT ?bp_label; separator = "|||") AS ?bioproject_ids ("^@" AS ?delimit3)
  GROUP_CONCAT(DISTINCT ?rs; separator = "|||") AS ?refseq_ids ("^@" AS ?delimit4)
  GROUP_CONCAT(DISTINCT ?label; separator = "|||") AS ?xref_ids
WHERE {
  {
    SELECT DISTINCT ?tax_id
    {
      GRAPH <http://togogenome.org/graph/tgtax> {
      ?child_tax rdfs:subClassOf ?tax_id
      }
    }
  }
  GRAPH <http://togogenome.org/graph/stats>
  {
    ?tax_id  rdfs:seeAlso/rdfs:seeAlso ?refseq .
  }
  GRAPH  <http://togogenome.org/graph/refseq>
  {
    ?refseq a insdc:Entry ;
      insdc:definition ?desc ;
      insdc:sequence_version ?rs ;
      insdc:dblink  ?bp .
    ?bp rdf:type insdc:BioProject ;
       rdfs:label ?bp_label .

    {
      ?refseq insdc:dblink ?xref .
      ?xref rdfs:label ?label ;
        rdf:type ?xref_type .
    }
    UNION
    {
      ?refseq rdfs:seeAlso ?xref .
      ?xref insdc:sequence_version ?label ;
        rdf:type ?xref_type .
      FILTER (?xref_type IN (insdc:RefSeq))
    }
    UNION
    {
      ?refseq rdfs:seeAlso ?xref .
      ?xref rdfs:label ?label ;
        rdf:type ?xref_type .
      FILTER (! ?xref_type IN (insdc:Entry, insdc:RefSeq))
    }
  }
} GROUP BY ?tax_id
;
